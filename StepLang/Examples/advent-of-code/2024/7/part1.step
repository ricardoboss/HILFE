string? input = fileRead("example.txt")
if (input == null) {
	println("Could not read file")
	return
}

list equations = []
foreach (string line in split(input, "\n")) {
	list parts = split(line, ": ")
	number result = toNumber(parts[0])
	list numbers = converted(split(parts[1], " "), (string x) { return toNumber(x) })
	doAdd(equations, {"result": result, "numbers": numbers})
}

function getResults = (list numbers) {
	list results = [doShift(numbers)]

	foreach (number n in numbers) {
		results = applyOperators(results, n)
	}

	return results
}

function applyOperators = (list numbers, number next) {
	list results = []

	foreach (number n in numbers) {
		doAdd(results, n + next)
		doAdd(results, n * next)
	}

	return results
}

number sumSolvable = 0
foreach (map equation in equations) {
	number expectedResult = equation["result"]
	list calculatedResults = getResults(equation["numbers"])

	if (contains(calculatedResults, expectedResult)) {
		sumSolvable += expectedResult
	}
}

println(sumSolvable)
